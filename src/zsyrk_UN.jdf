extern "C" %{
/*
 *  Copyright (c) 2010-2020
 *
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> z c d s
 *
 */
#include "dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

%}

uplo      [type = "dplasma_enum_t"]
trans     [type = "dplasma_enum_t"]
alpha     [type = "dplasma_complex64_t"]
descA     [type = "const parsec_tiled_matrix_t*"]
beta      [type = "dplasma_complex64_t"]
descC     [type = "parsec_tiled_matrix_t*"]

/* Look ahead on both dimensions */
lookX  [type = "int" hidden=on default="dplasma_aux_getGEMMLookahead(descC)"]


zsyrk(n, k) [ flops = inline_c %{ return FLOPS_ZSYRK(CLEAN_NB(descC, n), CLEAN_NB(descA, k)); %} ]
  /* Execution Space */
  n = 0..(descC->nt-1)
  k = 0..(descA->nt-1)

  /* Locality */
  : descC(n,n)

  READ  A    <- A zsyrk_in_data_A0(n, k)
  RW    C    <- ((0==k)) ? descC(n,n)
             <- ((k>=1)) ? C zsyrk(n, k-1)
             -> ((descA->nt>=(2+k))) ? C zsyrk(n, k+1)
             -> ((descA->nt==(k+1))) ? descC(n,n)

  CTL ctlx -> (k < (descA->nt-lookX)) ? ctlx zsyrk_in_data_A0(n, k+lookX)

BODY
{
    int tempnn = (n==(descC->nt-1)) ? (descC->n-(n*descC->nb)) : descC->nb;
    int tempkn = (k==(descA->nt-1)) ? (descA->n-(k*descA->nb)) : descA->nb;
    int ldan = BLKLDD( descA, n );
    dplasma_complex64_t zbeta = (k==0) ? beta : ((dplasma_complex64_t)1.);
    int ldcn = BLKLDD( descC, n );

    printlog("CORE_zsyrk(%d, %d)\n"
             "\t(uplo, trans, tempnn=%d, tempkn=%d, alpha, A(%d,%d)[%p], ldan=%d, zbeta, C(%d,%d)[%p], ldcn=%d)\n",
             n, k, tempnn, tempkn, n, k, A, ldan, n, n, C, ldcn );
    CORE_zsyrk(uplo, trans, tempnn, tempkn,
               alpha, A /* descA(n,k) */, ldan,
               zbeta, C /* descC(n,n) */, ldcn );
}
END

zsyrk_in_data_A0(n, k) [profile = off]
  /* Execution Space */
  n = 0..(descC->nt-1)
  k = 0..(descA->nt-1)

  /* Locality */
  : descA(n,k)

  READ  A    <- descA(n,k)
             -> A zsyrk(n, k)
  CTL ctlx   <- (k >= lookX) ? ctlx zsyrk(n, k-lookX)

BODY
{
    /* nothing */
}
END

zgemm(n, m, k) [ flops = inline_c %{ return FLOPS_ZGEMM(CLEAN_NB(descC, n), CLEAN_MB(descC, m), CLEAN_NB(descA, k)); %} ]
  /* Execution Space */
  n = 0..(descC->mt-2)
  m = (n+1)..(descC->mt-1)
  k = 0..(descA->nt-1)

  /* Locality */
  : descC(n,m)

  READ  A    <- A zgemm_in_data_A0(n, k)
  READ  B    <- B zgemm_in_data_A1(m, k)
  RW    C    <- ((0==k)) ? descC(n,m)
             <- ((k>=1)) ? C zgemm(n, m, k-1)
             -> ((descA->nt>=(k+2))) ? C zgemm(n, m, k+1)
             -> ((descA->nt==(k+1))) ? descC(n,m)

  CTL ctla -> (k < (descA->nt-lookX)) ? ctla zgemm_in_data_A1(m, k+lookX)
  CTL ctlb -> (k < (descA->nt-lookX)) ? ctlb zgemm_in_data_A0(n, k+lookX)

BODY
{
    int tempnn = (n==(descC->nt-1)) ? (descC->n-(n*descC->nb)) : descC->nb;
    int tempmm = ((m)==(descC->mt-1)) ? (descC->m-(m*descC->mb)) : descC->mb;
    int tempkn = (k==(descA->nt-1)) ? (descA->n-(k*descA->nb)) : descA->nb;
    int ldan = BLKLDD( descA, n );
    int ldam = BLKLDD( descA, m );
    dplasma_complex64_t zbeta = (k==0) ? beta : (dplasma_complex64_t)1.;
    int ldcn = BLKLDD( descC, n );

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(trans, dplasmaTrans, tempnn, tempmm, tempkn, zalpha, A(%d,%d)[%p], ldan, A(%d,%d)[%p], ldam, zbeta, C(%d,%d)[%p], ldcn)\n",
             n, m, k, n, k, A, m, k, B, n, m, C);
    CORE_zgemm(trans, dplasmaTrans, tempnn,
               tempmm, tempkn, alpha,
               A /* descA(n,k) */, ldan, B /* descA(m,k) */,
               ldam, zbeta, C /* descC(n,m) */,
               ldcn );
}
END

zgemm_in_data_A1(m, k) [profile = off]
  /* Execution Space */
  m = 1..(descC->mt-1) /* tight bound is (n+1)..(descC->mt-1) */
  k = 0..(descA->nt-1)

  /* Locality */
  : descA(m,k)

  READ  B    <- descA(m,k)
             -> B zgemm(0..(descC->mt-2), m, k)
  CTL ctla   <- (k >= lookX) ? ctla zgemm(0 .. (m-1), m, k-lookX)

BODY
{
    /* nothing */
}
END

zgemm_in_data_A0(n, k) [profile = off]
  /* Execution Space */
  n = 0..(descC->mt-2)
  k = 0..(descA->nt-1)

  /* Locality */
  : descA(n,k)

  READ  A    <- descA(n,k)
             -> A zgemm(n, (n+1)..(descC->mt-1), k)
  CTL ctlb   <- (k >= lookX) ? ctlb zgemm(n, (n+1) .. (descC->mt-1), k-lookX)

BODY
{
    /* nothing */
}
END
