extern "C" %{
/*
 * Copyright (c) 2010-2020 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

#if defined(DPLASMA_HAVE_CUDA)
#include <cublas.h>
#endif  /* defined(DPLASMA_HAVE_CUDA) */

%}

descA     [type = "parsec_tiled_matrix_t*"]

potrf_zpotrf(k)
  /* Execution Space */
  k = 0..(descA->mt-1)

  /* Locality */
  : descA(k,k)

  RW    A    <- (k == 0) ? descA(k, k) : C potrf_zherk(k-1, k)
             -> A potrf_ztrsm( k, (k+1)..(descA->mt-1))
             -> A trtri_ztrsmL(k,  0   ..(k-1))
             -> A trtri_ztrsmR(k, (k+1)..(descA->mt-1))
             -> A trtri_ztrtri(k)

; 44

BODY
{
    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );
    int info = 0;


    printlog("CORE_potrf_zpotrf(%d)\n"
             "\t(dplasmaLower, tempkm, A(%d,%d)[%p], ldak, descA->nb*k)\n",
             k, k, k, A);

    CORE_zpotrf(dplasmaLower, tempkm,
                A /* descA(k,k) */, ldak, &info );
}
END

potrf_ztrsm(k, m)
  /* Execution Space */
  k = 0    ..(descA->mt-2)
  m = (k+1)..(descA->mt-1)

  /* Locality */
  : descA(m,k)

  READ  A    <- A potrf_zpotrf(k)
  RW    B    <- (k == 0) ? descA(m, k) : C potrf_zgemm(k-1, m, k)
             -> A potrf_zherk(k, m)
             -> A potrf_zgemm(k, m, (k+1)..(m-1))
             -> B potrf_zgemm(k, (m+1)..(descA->mt-1), m)
             -> B trtri_ztrsmR(k, m)

; 40

BODY
{
    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );


    printlog("CORE_potrf_ztrsm(%d, %d)\n"
             "\t(dplasmaRight, dplasmaLower, dplasmaConjTrans, dplasmaNonUnit, tempmm, descA->mb, 1., A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldam)\n",
             k, m, k, k, A, m, k, B);

    CORE_ztrsm(dplasmaRight, dplasmaLower, dplasmaConjTrans, dplasmaNonUnit,
               tempmm, descA->mb,
               1., A /* descA(k,k) */, ldak,
                   B /* descA(m,k) */, ldam );
}
END

potrf_zherk(k, m)
  /* Execution Space */
  k = 0    ..(descA->mt-2)
  m = (k+1)..(descA->mt-1)

  /* Locality */
  : descA(m,m)

  READ  A    <- B potrf_ztrsm(k, m)
  RW    C    <- (k == 0) ? descA(m, m) : C potrf_zherk(k-1, m)
             -> (m == (k+1)) ? A potrf_zpotrf(m) : C potrf_zherk(k+1, m)

  CTL   ctl0 -> ctl0 trtri_ztrsmR(k, m)

; 40

BODY
{
    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int ldam = BLKLDD( descA, m );


    printlog("CORE_potrf_zherk(%d, %d)\n"
             "\t(dplasmaLower, dplasmaNoTrans, tempmm, descA->mb, -1., A(%d,%d)[%p], ldam, 1., A(%d,%d)[%p], ldam)\n",
             k, m, m, k, A, m, m, C);

    CORE_zherk(dplasmaLower, dplasmaNoTrans,
               tempmm, descA->mb,
               -1., A /* descA(m,k) */, ldam,
                1., C /* descA(m,m) */, ldam );
}
END

potrf_zgemm(k, m, n)
  /* Execution Space */
  k = 0    ..(descA->mt-2)
  m = (k+2)..(descA->mt-1)
  n = (k+1)..(m-1)

  /* Locality */
  : descA(m,n)

  READ  A    <- B potrf_ztrsm(k, m)
  READ  B    <- B potrf_ztrsm(k, n)
  RW    C    <- (k == 0) ? descA(m, n) : C potrf_zgemm(k-1, m, n)
             -> (n == (k+1)) ? B potrf_ztrsm(n, m) : C potrf_zgemm(k+1, m, n)

  CTL   ctl1 -> ctl1 trtri_ztrsmR(k, m)
  CTL   ctl2 -> ctl2 trtri_ztrsmR(k, n)

; 40

BODY [type=CUDA]
{
#if defined(PRECISION_z) || defined(PRECISION_c)
    cuDoubleComplex zone  = make_cuDoubleComplex( 1., 0.);
    cuDoubleComplex mzone = make_cuDoubleComplex(-1., 0.);
#else
    double zone  =  1.;
    double mzone = -1.;
#endif

    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int ldam = BLKLDD( descA, m );
    int ldan = BLKLDD( descA, n );

    cublasStatus_t status;
    cublasSetKernelStream( parsec_body.stream );
    cublasZgemm( 'N', dplasma_lapack_const(dplasmaConjTrans),
                 tempmm, descA->mb, descA->mb,
                 mzone, (cuDoubleComplex*)A, ldam,
                        (cuDoubleComplex*)B, ldan,
                 zone,  (cuDoubleComplex*)C, ldam );
    status = cublasGetError();
    PARSEC_CUDA_CHECK_ERROR( "cublasZgemm ", status, {return PARSEC_HOOK_RETURN_ERROR;} );
}
END

BODY
{
    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int ldam = BLKLDD( descA, m );
    int ldan = BLKLDD( descA, n );

    printlog("CORE_potrf_zgemm(%d, %d, %d)\n"
             "\t(dplasmaNoTrans, dplasmaConjTrans, tempmm, descA->mb, descA->mb, -1., A(%d,%d)[%p], ldam, A(%d,%d)[%p], descA->mb, 1., A(%d,%d)[%p], ldam)\n",
             k, m, n, m, k, A, n, k, B, m, n, C);

    CORE_zgemm(dplasmaNoTrans, dplasmaConjTrans,
               tempmm, descA->mb, descA->mb,
               -1., A /* descA(m,k) */, ldam,
                    B /* descA(n,k) */, ldan,
                1., C /* descA(m,n) */, ldam );
}
END

trtri_ztrsmR(k, m)
  /* Execution Space */
  k = 0    ..(descA->mt-2)
  m = (k+1)..(descA->mt-1)

  /* Locality */
  : descA(m,k)

  READ  A    <- A potrf_zpotrf(k)
  RW    B    <- B potrf_ztrsm(k, m)
             -> (m == (k+1)) ? B trtri_zgemm(m, (m+1)..(descA->mt-1), m-1)
             -> (m == (k+1)) ? B trtri_ztrsmL(m, m-1)
             -> (m >  (k+1)) ? C trtri_zgemm(k+1, m, k)
             -> A trtri_zgemm(k, m, 0..(k-1))

  CTL   ctl0 <- ctl0 potrf_zherk(k, m)
  CTL   ctl1 <- ctl1 potrf_zgemm(k, m, (k+1)..(m-1))
  CTL   ctl2 <- ctl2 potrf_zgemm(k, (m+1)..(descA->mt-1), m)
  CTL   ctl3 -> (k == 0) ? ctl3 trtri_ztrtri(k)

; 30

BODY
{
    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int tempkn = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );


    printlog("CORE_trtri_ztrsmR(%d, %d)\n"
             "\t(dplasmaRight, dplasmaLower, dplasmaNoTrans, dplasmaNonUnit, tempmm, tempkn, -1., A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldam)\n",
             k, m, k, k, A, m, k, B);

    CORE_ztrsm(dplasmaRight, dplasmaLower, dplasmaNoTrans, dplasmaNonUnit,
               tempmm, tempkn,
               -1., A /* descA(k,k) */, ldak,
                    B /* descA(m,k) */, ldam );
}
END

trtri_zgemm(k, m, n)
  /* Execution Space */
  k = 1    ..(descA->nt-2)
  m = (k+1)..(descA->nt-1)
  n = 0    ..(k-1)

  /* Locality */
  : descA(m,n)

  READ  A    <- B trtri_ztrsmR(k, m)
  READ  B    <- (k == (n+1)) ? B trtri_ztrsmR(k-1, k) : C trtri_zgemm(k-1, k, n)
  RW    C    <- (k == (n+1)) ? B trtri_ztrsmR(k-1, m) : C trtri_zgemm(k-1, m, n)
             -> (m == (k+1)) ? B trtri_ztrsmL(k+1, n) : C trtri_zgemm(k+1, m, n)
             -> (m == (k+1)) ? B trtri_zgemm(k+1, (k+2)..(descA->mt-1), n)

  CTL   ctl4 -> (m == (k+1)) ? ctl4 trtri_ztrsmL(k+1, k)
  CTL   ctl5 <- ctl5 trtri_zgemm(n, m, 0..(n-1))
             -> ctl5 trtri_zgemm((k+1)..(m-1), m, k)
  CTL   ctl6 -> ctl6 trtri_ztrsmL(k, n)

; 30

BODY [type=CUDA]
{
#if defined(PRECISION_z) || defined(PRECISION_c)
    cuDoubleComplex zone  = make_cuDoubleComplex( 1., 0.);
#else
    double zone  =  1.;
#endif

    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int tempkn = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldam = BLKLDD( descA, m );
    int ldak = BLKLDD( descA, k );

    cublasStatus_t status;
    cublasSetKernelStream( parsec_body.stream );
    cublasZgemm( 'N', 'N',
                 tempmm, descA->nb, tempkn,
                 zone, (cuDoubleComplex*)A, ldam,
                       (cuDoubleComplex*)B, ldak,
                 zone, (cuDoubleComplex*)C, ldam );
    status = cublasGetError();
    PARSEC_CUDA_CHECK_ERROR( "cublasZgemm ", status, {return PARSEC_HOOK_RETURN_ERROR;} );
}
END

BODY
{
    int tempmm = (m == (descA->mt-1)) ? (descA->m - m * descA->mb) : descA->mb;
    int tempkn = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldam = BLKLDD( descA, m );
    int ldak = BLKLDD( descA, k );

    printlog("CORE_trtri_zgemm(%d, %d, %d)\n"
             "\t(dplasmaNoTrans, dplasmaNoTrans, tempmm, descA->nb, tempkn, 1., A(%d,%d)[%p], ldam, A(%d,%d)[%p], ldak, 1., A(%d,%d)[%p], ldam)\n",
             k, m, n, m, k, A, k, n, B, m, n, C);

    CORE_zgemm(dplasmaNoTrans, dplasmaNoTrans,
               tempmm, descA->nb, tempkn,
               1., A /* descA(m,k) */, ldam,
                   B /* descA(k,n) */, ldak,
               1., C /* descA(m,n) */, ldam );
}
END

trtri_ztrsmL(k, n)
  /* Execution Space */
  k = 1..(descA->nt-1)
  n = 0..(k-1)

  /* Locality */
  : descA(k,n)

  READ  A    <- A potrf_zpotrf(k)
  RW    B    <- (k == (n+1)) ? B trtri_ztrsmR(k-1, k) : C trtri_zgemm(k-1, k, n)
             -> A lauum_zherk(k, n)
             -> B lauum_ztrmm(k, n)
             -> B lauum_zgemm(k, n, (n+1)..(k-1))
             -> A lauum_zgemm(k, 0..(n-1), n)

  CTL   ctl4 <- (k == (n+1)) ? ctl4 trtri_zgemm(k-1, k, 0..(k-2))
  CTL   ctl6 <- ctl6 trtri_zgemm(k, (k+1)..(descA->mt-1), n)
  CTL   ctl7 -> ctl7 trtri_ztrtri(k)

; 30

BODY
{
    int tempkn = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldak = BLKLDD( descA, k );


    printlog("CORE_trtri_ztrsmL(%d, %d)\n"
             "\t(dplasmaLeft, dplasmaLower, dplasmaNoTrans, dplasmaNonUnit, tempkn, descA->nb, 1., A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldak)\n",
             k, n, k, k, A, k, n, B);

    CORE_ztrsm(dplasmaLeft, dplasmaLower, dplasmaNoTrans, dplasmaNonUnit,
               tempkn, descA->nb,
               1., A /* descA(k,k) */, ldak,
                   B /* descA(k,n) */, ldak );
}
END

trtri_ztrtri(k)
  /* Execution Space */
  k = 0..(descA->nt-1)

  /* Locality */
  : descA(k,k)

  RW    A    <- A potrf_zpotrf(k)
             -> A lauum_zlauum(k)
             -> A lauum_ztrmm(k, 0..(k-1))

  CTL   ctl3 <- (k == 0) ? ctl3 trtri_ztrsmR(0, 1..(descA->mt-1))
  CTL   ctl7 <- (k >  0) ? ctl7 trtri_ztrsmL(k, 0..(k-1))

; 30

BODY
{
    int tempkn = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldak = BLKLDD( descA, k );
    int info = 0;


    printlog("CORE_trtri_ztrtri(%d)\n"
             "\t(dplasmaLower, dplasmaNonUnit, tempkn, A(%d,%d)[%p], ldak, descA->nb*k)\n",
             k, k, k, A);

    CORE_ztrtri(dplasmaLower, dplasmaNonUnit, tempkn,
                A /* descA(k,k) */, ldak, &info );
}
END

lauum_zherk(k, n)
  /* Execution Space */
  k = 1..(descA->mt-1)
  n = 0..(k-1)

  /* Locality */
  : descA(n,n)

  READ  A    <- B trtri_ztrsmL(k, n)
  RW    C    <- (k == (n+1)) ? A lauum_zlauum(k-1) : C lauum_zherk(k-1, n)
             -> (k <  (descA->nt-1)) ? C lauum_zherk(k+1, n)
             -> (k == (descA->nt-1)) ? descA(n, n)

  CTL   ctl8 -> ctl8 lauum_ztrmm(k, n)

; 20

BODY
{
    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );
    int ldan = BLKLDD( descA, n );


    printlog("CORE_zherk(%d, %d)\n"
             "\t(dplasmaLower, dplasmaConjTrans, descA->mb, tempkm, 1., A(%d,%d)[%p], ldak, 1., A(%d,%d)[%p], ldan)\n",
             k, n, k, n, A, n, n, C);

    CORE_zherk(dplasmaLower, dplasmaConjTrans,
               descA->mb, tempkm,
               1., A /* descA(k,n) */, ldak,
               1., C /* descA(n,n) */, ldan );
}
END

lauum_zgemm(k, n, m)
  /* Execution Space */
  k = 2    ..(descA->mt-1)
  n = 0    ..(k-2)
  m = (n+1)..(k-1)

  /* Locality */
  : descA(m,n)

  READ  A    <- B trtri_ztrsmL(k, m)
  READ  B    <- B trtri_ztrsmL(k, n)
  RW    C    <- (k == (m+1)) ? B lauum_ztrmm(k-1, n) : C lauum_zgemm(k-1, n, m)
             -> (k == (descA->nt-1)) ? descA(m, n)
             -> (k <  (descA->nt-1)) ? C lauum_zgemm(k+1, n, m)

  CTL   ctl9  -> ctl9  lauum_ztrmm(k, n)
  CTL   ctl10 -> ctl10 lauum_ztrmm(k, m)

; 20

BODY [type=CUDA]
{
#if defined(PRECISION_z) || defined(PRECISION_c)
    cuDoubleComplex zone  = make_cuDoubleComplex( 1., 0.);
#else
    double zone  =  1.;
#endif

    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );

    cublasStatus_t status;
    cublasSetKernelStream( parsec_body.stream );
    cublasZgemm( dplasma_lapack_const(dplasmaConjTrans), 'N',
                 descA->mb, descA->nb, tempkm,
                 zone, (cuDoubleComplex*)A, ldak,
                       (cuDoubleComplex*)B, ldak,
                 zone, (cuDoubleComplex*)C, ldam );
    status = cublasGetError();
    PARSEC_CUDA_CHECK_ERROR( "cublasZgemm ", status, {return PARSEC_HOOK_RETURN_ERROR;} );
}
END

BODY
{
    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(dplasmaConjTrans, dplasmaNoTrans, descA->mb, descA->nb, tempkm, 1., A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldak, 1., A(%d,%d)[%p], ldam)\n",
             k, n, m, k, m, A, k, n, B, m, n, C);

    CORE_zgemm(dplasmaConjTrans, dplasmaNoTrans,
               descA->mb, descA->nb, tempkm,
               1., A /* descA(k,m) */, ldak,
                   B /* descA(k,n) */, ldak,
               1., C /* descA(m,n) */, ldam );
}
END

lauum_ztrmm(k, n)
  /* Execution Space */
  k = 1..(descA->mt-1)
  n = 0..(k-1)

  /* Locality */
  : descA(k,n)

  READ  A    <- A trtri_ztrtri(k)
  RW    B    <- B trtri_ztrsmL(k, n)
             -> (k == (descA->nt-1)) ? descA(k, n)
             -> (k <  (descA->nt-1)) ? C lauum_zgemm(k+1, n, k)

  CTL   ctl8  <- ctl8  lauum_zherk(k, n)
  CTL   ctl9  <- ctl9  lauum_zgemm(k, n, (n+1)..(k-1))
  CTL   ctl10 <- ctl10 lauum_zgemm(k, 0..(n-1), n)
  CTL   ctl11 -> ctl11 lauum_zlauum(k)

; 20

BODY
{
    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );


    printlog("CORE_ztrmm(%d, %d)\n"
             "\t(dplasmaLeft, dplasmaLower, dplasmaConjTrans, dplasmaNonUnit, tempkm, descA->nb, 1., A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldak)\n",
             k, n, k, k, A, k, n, B);

    CORE_ztrmm(dplasmaLeft, dplasmaLower, dplasmaConjTrans, dplasmaNonUnit,
               tempkm, descA->nb,
               1., A /* descA(k,k) */, ldak,
                   B /* descA(k,n) */, ldak );
}
END

lauum_zlauum(k)
  /* Execution Space */
  k = 0..(descA->mt-1)

  /* Locality */
  : descA(k,k)

  RW    A    <- A trtri_ztrtri(k)
             -> (k == (descA->nt-1)) ? descA(k, k)
             -> (k <  (descA->nt-1)) ? C lauum_zherk(k+1, k)
  CTL  ctl11 <- ctl11 lauum_ztrmm(k, 0..(k-1))

; 20

BODY
{
    int tempkm = (k == (descA->mt-1)) ? (descA->m - k * descA->mb) : descA->mb;
    int ldak = BLKLDD( descA, k );


    printlog("CORE_zlauum(%d)\n"
             "\t(dplasmaLower, tempkm, A(%d,%d)[%p], ldak)\n",
             k, k, k, A);

    CORE_zlauum(dplasmaLower, tempkm,
                A /* descA(k,k) */, ldak );
}
END

