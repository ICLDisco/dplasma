extern "C" %{
/*
 * Copyright (c) 2010-2020 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 *
 * @precisions normal z -> s d c
 * $COPYRIGHT
 *
 */
#include "dplasmajdf.h"
#include "dplasmaaux.h"
#include "parsec/data_dist/matrix/matrix.h"
#include "parsec/data_dist/matrix/two_dim_rectangle_cyclic.h"
#include <math.h>

#if defined(DPLASMA_HAVE_CUDA)
#include <cublas.h>
#endif  /* defined(DPLASMA_HAVE_CUDA) */

/* Define the different shapes this JDF is using */
#define A_SHAPE 0
#define B_SHAPE 1
#define C_SHAPE 2

/* Assume the functions on type & type_remote will return parsec_arena_datatype_t */
#define JDF2C_TYPE_ADT_NOT_INDEX

/* Include the functions to obtain the parsec_arena_datatype_t */
#include "dplasmajdf_lapack_dtt.h"
//#define FULL_CONVERSION
#ifdef FULL_CONVERSION
#define ADTT_READ(dM, loc, shape, layout) ADTT_DC(dM, loc, shape, layout)
#else
#define ADTT_READ(dM, loc, shape, layout) ADTT_DC(dM, loc, shape, LAPACK)
#endif

/* Note: with this approach to support LAPACK format and minimize
 * the number of tile conversions performed, it is critical to
 * determined the correct location on the matrix where the tile was
 * originated from.
 * Note that the LOC parameters do not correspond to the binding of the
 * task to the matrix (although sometimes they match). These parameters
 * correspond to the location were the datacopy was originate on the matrix (maybe
 * it was read on another task).
 * If there is no possibility to determine it (datacopy passed along multiple
 * tasks and that information is lost) the approach is to force a reshapping.
 *
 */

#include "zgemm_TT_summa.h"

%}

/*
 * Globals
 */

/* Keep this first, as in all jdf in this directory, to
 * enable switching between GEMM implementations.
 */
gemm_type [ type = int hidden=on default="DPLASMA_ZGEMM_TT_SUMMA" ]

transA [type = int]
transB [type = int]

alpha  [type = dplasma_complex64_t]
beta   [type = dplasma_complex64_t]

ddescA     [type = "dplasma_data_collection_t*"]
descA      [type = "parsec_tiled_matrix_t*" hidden = on default = "((dplasma_data_collection_t*)ddescA)->dc_original" aligned=ddescA]

ddescB     [type = "dplasma_data_collection_t*"]
descB      [type = "parsec_tiled_matrix_t*" hidden = on default = "((dplasma_data_collection_t*)ddescB)->dc_original" aligned=ddescB]

ddescC     [type = "dplasma_data_collection_t*"]
descC      [type = "parsec_tiled_matrix_t*" hidden = on default = "((dplasma_data_collection_t*)ddescC)->dc_original" aligned=ddescC]

Cdist  [type = "parsec_data_collection_t *"]

P      [type = "int" hidden=on default="((parsec_matrix_block_cyclic_t*)descC)->grid.rows"]
Q      [type = "int" hidden=on default="((parsec_matrix_block_cyclic_t*)descC)->grid.cols"]

/* Look ahead on both dimensions */
lookP  [type = "int" hidden=on default="dplasma_aux_getGEMMLookahead(descC)"]
lookQ  [type = "int" hidden=on default="dplasma_aux_getGEMMLookahead(descC)"]

/**************************************************
 *                       READ_A                   *
 **************************************************/
READ_A(k, m)  [profile = off]

k = 0 .. descA->mt-1
m = 0 .. descA->nt-1

loc_A = %{ return LOC(descA, k, m); %}

: descA(k, m)

READ A <- ddescA(k, m)        [ type        = %{ return ADTT_READ(ddescA, loc_A, A_SHAPE, TILED); %}
                                type_data   = %{ return ADTT_READ(ddescA, loc_A, A_SHAPE, LAPACK); %} ]
       -> A RING_A(k, m, k%Q) /* dep OUT: rely on datacopy dtt for sending */
BODY
{
    printlog("rank %u <- A(%d,%d)\n", ((parsec_data_collection_t*)descA)->myrank, k, m);
}
END

RING_A(k, m, q)  [profile = off]

k = 0 .. descA->mt-1
m = 0 .. descA->nt-1
q = 0 .. Q-1
prevq = (q-1+Q)%Q
nextq = (q+1)%Q

loc_A = %{ return LOC(descA, k, m); %}

: Cdist(m, q)

READ A <- (k%Q == q) ? A READ_A(k, m) : A RING_A(k, m, prevq) [ type_remote = %{ return ADTT_DC(ddescA, loc_A, A_SHAPE, TILED); %} ]
       -> A GEMM(m, q .. descC->nt-1 .. Q, k)                 /* dep OUT: rely on datacopy dtt for sending */
       -> (nextq != (k%Q)) ? A RING_A(k, m, nextq)            /* dep OUT: rely on datacopy dtt for sending */

CTL ctla <- (k >= lookQ) ? ctla GEMM(m, q .. descC->nt-1 .. Q, k-lookQ)

BODY
{
    printlog("rank %u <- A(%d,%d)\n", ((parsec_data_collection_t*)descA)->myrank, k, m);
}
END

/**************************************************
 *                       READ_B                   *
 **************************************************/
READ_B(n, k) [profile = off]

n = 0 .. descB->mt-1
k = 0 .. descB->nt-1

loc_B = %{ return LOC(descB, n, k); %}

: descB(n, k)

READ B <- ddescB(n, k)        [ type        = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, TILED); %}
                                type_data   = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, LAPACK); %} ]
       -> B RING_B(n, k, k%P) /* dep OUT: rely on datacopy dtt for sending */
BODY
{
     printlog("rank %u <- B(%d,%d)\n", ((parsec_data_collection_t*)descB)->myrank, n, k);
}
END

RING_B(n, k, p)  [profile = off]

n = 0 .. descB->mt-1
k = 0 .. descB->nt-1
p = 0 .. P-1
prevp = (p-1+P)%P
nextp = (p+1)%P

loc_B = %{ return LOC(descB, n, k); %}

: Cdist(p, n)

READ B <- (k%P == p) ? B READ_B(n, k) : B RING_B(n, k, prevp) [ type_remote = %{ return ADTT_DC(ddescB, loc_B, B_SHAPE, TILED); %} ]
       -> B GEMM(p .. descC->mt-1 .. P, n, k)                 /* dep OUT: rely on datacopy dtt for sending */
       -> (nextp != (k%P)) ? B RING_B(n, k, nextp)            /* dep OUT: rely on datacopy dtt for sending */

CTL ctlb <- (k >= lookP) ? ctlb GEMM(p .. descC->mt-1 .. P, n, k-lookP)

BODY
{
     printlog("rank %u <- B(%d,%d)\n", ((parsec_data_collection_t*)descB)->myrank, n, k);
}
END

/**************************************************
 *                       GEMM                     *
 **************************************************/
GEMM(m, n, k) [flops = inline_c%{ return FLOPS_ZGEMM(CLEAN_MB(descC, m), CLEAN_NB(descC, n), CLEAN_MB(descA, k)); %}]

// Execution space
m = 0 .. descC->mt-1
n = 0 .. descC->nt-1
k = 0 .. descA->mt-1

loc_A = %{ return LOC(descA, k, m); %}
loc_B = %{ return LOC(descB, n, k); %}
loc_C = %{ return LOC(descC, m, n); %}

// Parallel partitioning
: descC(m, n)

// Parameters
READ A <- A RING_A(k, m, n%Q)                         [ type_remote = %{ return ADTT_DC(ddescA, loc_A, A_SHAPE, TILED); %} ]
READ B <- B RING_B(n, k, m%P)                         [ type_remote = %{ return ADTT_DC(ddescB, loc_B, B_SHAPE, TILED); %} ]
RW   C <- (k == 0)             ? ddescC(m, n)         [ type        = %{ return ADTT_READ(ddescC, loc_C, C_SHAPE, TILED); %}
                                                        type_data   = %{ return ADTT_READ(ddescC, loc_C, C_SHAPE, LAPACK); %} ]
       <- (k != 0)             ? C GEMM( m, n, k-1 )  [ type_remote = %{ return ADTT_DC(ddescC, loc_C, C_SHAPE, TILED); %} ]
       -> (k == (descA->mt-1)) ? ddescC(m, n)         [ type        = %{ return ADTT_CP(_f_C, ddescC, loc_C, C_SHAPE); %}
                                                        type_data   = %{ return ADTT_DC(ddescC, loc_C, C_SHAPE, LAPACK); %} ]
       -> (k != (descA->mt-1)) ? C GEMM( m, n, k+1 )  /* dep OUT: rely on datacopy dtt for sending */

CTL ctla -> (k < (descA->mt-lookQ)) ? ctla RING_A(k+lookQ, m, n%Q)
CTL ctlb -> (k < (descA->mt-lookP)) ? ctlb RING_B(n, k+lookP, m%P)

BODY [type=CUDA
      weight=(descA->mt-k)
      A.size=%{ return descA->mb*descA->nb*parsec_datadist_getsizeoftype(descA->mtype);%}
      B.size=%{ return descB->mb*descB->nb*parsec_datadist_getsizeoftype(descB->mtype);%}
      C.size=%{ return descC->mb*descC->nb*parsec_datadist_getsizeoftype(descC->mtype);%}
      A.dc=ddescA B.dc=ddescB C.dc=ddescC
      stage_in=stage_in_lapack
      stage_out=stage_out_lapack]
{
#if defined(PRECISION_z) || defined(PRECISION_c)
    cuDoubleComplex lalpha = make_cuDoubleComplex(creal(alpha), cimag(alpha));
    cuDoubleComplex lbeta  = (k == 0) ? make_cuDoubleComplex(creal(beta), cimag(beta))
                                      : make_cuDoubleComplex(1.0, 0.0);
#else
    double lalpha = alpha;
    double lbeta  = (k == 0) ? beta : 1.0;
#endif
    int tempmm = m == descC->mt-1 ? descC->m - m * descC->mb : descC->mb;
    int tempnn = n == descC->nt-1 ? descC->n - n * descC->nb : descC->nb;
    int tempkk = k == descA->mt-1 ? descA->m - k * descA->mb : descA->mb;
    int ldam = descA->mb;
    int ldbn = descB->mb;
    int ldcm = descC->mb;

    cublasStatus_t status;
    cublasSetKernelStream( parsec_body.stream );
    cublasZgemm( dplasma_lapack_const(transA), dplasma_lapack_const(transB),
             tempmm, tempnn, tempkk,
             lalpha, (cuDoubleComplex*)A, ldam,
                     (cuDoubleComplex*)B, ldbn,
             lbeta,  (cuDoubleComplex*)C, ldcm );
    status = cublasGetError();
    if(status != CUBLAS_STATUS_SUCCESS){
      printf("ISSUE\n");
    }
    PARSEC_CUDA_CHECK_ERROR( "cublasZgemm ", status,
                            {return -1;} );
}
END

BODY
{
    dplasma_complex64_t lbeta = (k == 0) ? beta : (dplasma_complex64_t)1.0;
    int tempmm = m == descC->mt-1 ? descC->m - m * descC->mb : descC->mb;
    int tempnn = n == descC->nt-1 ? descC->n - n * descC->nb : descC->nb;
    int tempkk = k == descA->mt-1 ? descA->m - k * descA->mb : descA->mb;
    int ldak = LDA(ddescA, A);
    int ldbn = LDA(ddescB, B);
    int ldcm = LDA(ddescC, C);

    CORE_zgemm(transA, transB,
               tempmm, tempnn, tempkk,
               alpha, A /*A(k, m)*/, ldak,
                      B /*B(n, k)*/, ldbn,
               lbeta, C /*C(m, n)*/, ldcm);

    printlog("gemm( %d, %d, %d )\n"
             "    ( %s, %s, %d, %d, %d, %f, A(%d,%d), %d, B(%d,%d), %d, %f, C(%d,%d), %d)\n",
             m, n, k,
             &dplasma_lapack_const( transA ), &dplasma_lapack_const( transB ),
             tempmm, tempnn, tempkk,
             creal(alpha), k, m, ldak,
                           n, k, ldbn,
             creal(lbeta), m, n, ldcm);
}
END
