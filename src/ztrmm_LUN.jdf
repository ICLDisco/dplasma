extern "C" %{
/*
 * Copyright (c) 2010-2020 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

/* Define the different shapes this JDF is using */
#define A_SHAPE 0
#define B_SHAPE 1

/* Assume the functions on type & type_remote will return parsec_arena_datatype_t */
#define JDF2C_TYPE_ADT_NOT_INDEX

/* Include the functions to obtain the parsec_arena_datatype_t */
#include "dplasmajdf_lapack_dtt.h"
//#define FULL_CONVERSION
#ifdef FULL_CONVERSION
#define ADTT_READ(dM, loc, shape, layout) ADTT_DC(dM, loc, shape, layout)
#else
#define ADTT_READ(dM, loc, shape, layout) ADTT_DC(dM, loc, shape, LAPACK)
#endif

/* Note: with this approach to support LAPACK format and minimize
 * the number of tile conversions performed, it is critical to
 * determined the correct location on the matrix where the tile was
 * originated from.
 * Note that the LOC parameters do not correspond to the binding of the
 * task to the matrix (although sometimes they match). These parameters
 * correspond to the location were the datacopy was originate on the matrix (maybe
 * it was read on another task).
 * If there is no possibility to determine it (datacopy passed along multiple
 * tasks and that information is lost) the approach is to force a reshapping.
 *
 */

%}

side      [type = "dplasma_enum_t"]
uplo      [type = "dplasma_enum_t"]
trans     [type = "dplasma_enum_t"]
diag      [type = "dplasma_enum_t"]
alpha     [type = "dplasma_complex64_t"]

ddescA     [type = "dplasma_data_collection_t*"]
descA      [type = "const parsec_tiled_matrix_dc_t*" hidden = on default = "((dplasma_data_collection_t*)ddescA)->dc_original" aligned=ddescA]

ddescB     [type = "dplasma_data_collection_t*"]
descB      [type = "parsec_tiled_matrix_dc_t*" hidden = on default = "((dplasma_data_collection_t*)ddescB)->dc_original" aligned=ddescB]

read_A(m, k) [profile = off]
  /* Execution Space */
  m = 0 .. (descB->mt-1)
  k = m .. (descA->mt-1)

loc_A = %{ return LOC(descA, m, k); %}

  /* Locality */
  : descA(m,k)

  READ  A    <- ddescA(m,k)                                [ type        = %{ return ADTT_READ(ddescA, loc_A, A_SHAPE, TILED); %}
                                                             type_data   = %{ return ADTT_READ(ddescA, loc_A, A_SHAPE, LAPACK); %} ]
             -> (m == k) ? A ztrmm(m, 0..(descB->nt-1))    /* dep OUT: rely on datacopy dtt for sending */
             -> (m != k) ? A zgemm(m, 0..(descB->nt-1), k) /* dep OUT: rely on datacopy dtt for sending */

BODY
{
    /* nothing */
}
END

read_B(n, k) [profile = off]
  /* Execution Space */
  n = 0..(descB->nt-1)
  k = 1..(descB->mt-1)

loc_B = %{ return LOC(descB, k, n); %}

  /* Locality */
  : descB(k,n)

  READ  B    <- ddescB(k,n)             [ type        = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, TILED); %}
                                          type_data   = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, LAPACK); %} ]
             -> B zgemm(0..(k-1), n, k) /* dep OUT: rely on datacopy dtt for sending */

BODY
{
    /* nothing */
}
END

ztrmm(m, n) [ flops = inline_c %{ return FLOPS_ZTRMM(side, CLEAN_MB(descB, m), CLEAN_NB(descB, n)); %} ]
  /* Execution Space */
  m = 0..(descB->mt-1)
  n = 0..(descB->nt-1)

loc_A = %{ return LOC(descA, m, m); %}
loc_B = %{ return LOC(descB, m, n); %}

  /* Locality */
  : descB(m,n)

  CTL   ctl0 <- ctl0 zgemm(0..(m-1), n, m)
  READ  A    <- A read_A(m, m)                            [ type_remote = %{ return ADTT_DC(ddescA, loc_A, A_SHAPE, TILED); %} ]
  RW    B    <- ddescB(m,n)                               [ type        = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, TILED); %}
                                                            type_data   = %{ return ADTT_READ(ddescB, loc_B, B_SHAPE, LAPACK); %} ]
             -> (m == (descB->mt-1)) ? ddescB(m,n)        [ type        = %{ return ADTT_CP(_f_B, ddescB, loc_B, B_SHAPE); %}
                                                            type_data   = %{ return ADTT_DC(ddescB, loc_B, B_SHAPE, LAPACK); %} ]
             -> (m <  (descB->mt-1)) ? C zgemm(m, n, m+1) /* dep OUT: rely on datacopy dtt for sending */

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempnn = ((n)==(descB->nt-1)) ? (descB->n-(n*descB->nb)) : descB->nb;
    int lda = LDA(ddescA, A);
    int ldb = LDA(ddescB, B);

    printlog("CORE_ztrmm(%d, %d)\n"
             "\t(side, uplo, trans, diag, tempmm, tempnn, alpha, A(%d,%d)[%p], lda, B(%d,%d)[%p], ldb)\n",
             m, n, m, m, A, m, n, B);

    CORE_ztrmm(side, uplo, trans,
               diag, tempmm, tempnn,
               alpha, A /* descA(m,m) */, lda,
                      B /* descB(m,n) */, ldb );
}
END

zgemm(m, n, k) [ flops = inline_c %{ return FLOPS_ZGEMM(CLEAN_MB(descB, m), CLEAN_NB(descB, n), CLEAN_NB(descA, k)); %} ]
  /* Execution Space */
  m = 0..(descB->mt-2)
  n = 0..(descB->nt-1)
  k = (m+1)..(descB->mt-1)

loc_A = %{ return LOC(descA, m, k); %}
loc_B = %{ return LOC(descB, k, n); %}
loc_C = %{ return LOC(descB, m, n); %}

  /* Locality */
  : descB(m,n)

  CTL   ctl0 -> ctl0 ztrmm(k, n)
  READ  A    <- A read_A(m, k)                            [ type_remote = %{ return ADTT_DC(ddescA, loc_A, A_SHAPE, TILED); %} ]
  READ  B    <- B read_B(n, k)                            [ type_remote = %{ return ADTT_DC(ddescB, loc_B, B_SHAPE, TILED); %} ]
  RW    C    <- (k >  (m+1)) ? C zgemm(m, n, k-1)         [ type_remote = %{ return ADTT_DC(ddescB, loc_C, B_SHAPE, TILED); %} ]
             <- (k == (m+1)) ? B ztrmm(k-1, n)            [ type_remote = %{ return ADTT_DC(ddescB, loc_C, B_SHAPE, TILED); %} ]
             -> (k == (descB->mt-1)) ? ddescB(m,n)        [ type        = %{ return ADTT_CP(_f_C, ddescB, loc_C, B_SHAPE); %}
                                                            type_data   = %{ return ADTT_DC(ddescB, loc_C, B_SHAPE, LAPACK); %} ]
             -> (k <  (descB->mt-1)) ? C zgemm(m, n, k+1) /* dep OUT: rely on datacopy dtt for sending */

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempnn = ((n)==(descB->nt-1)) ? (descB->n-(n*descB->nb)) : descB->nb;
    int tempkn = ((k)==(descA->nt-1)) ? (descA->n-(k*descA->nb)) : descA->nb;
    int lda = LDA(ddescA, A);
    int ldb = LDA(ddescB, B);
    int ldc = LDA(ddescB, C);

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(trans, dplasmaNoTrans, tempmm, tempnn, tempkn, alpha, A(%d,%d)[%p], lda, B(%d,%d)[%p], ldbk, 1.000000, B(%d,%d)[%p], ldb)\n",
             m, n, k, m, k, A, k, n, B, m, n, C);

    CORE_zgemm(trans, dplasmaNoTrans, tempmm, tempnn, tempkn,
               alpha, A /* descA(m,k) */, lda,
                      B /* descB(k,n) */, ldb,
               1.0,   C /* descB(m,n) */, ldc );
}
END

